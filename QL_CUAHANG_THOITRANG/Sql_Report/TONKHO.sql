CREATE PROCEDURE InBCTonKho
    @IDSP NCHAR(10) = NULL
AS
BEGIN
    SELECT 
        SP.IDSP,
        SP.TENSP,
        SZ.TENSIZE,
        M.TENMAU,
        ISNULL(NHAP.TONGNHAP, 0) AS NHAPTRONGKY,
        ISNULL(XUAT.TONGXUAT, 0) AS XUATTRONGKY,
        ISNULL(NHAP.TONGNHAP, 0) - ISNULL(XUAT.TONGXUAT, 0) AS TONCUOIKY,
        SP.DGNHAP,
        SP.DGBAN,
        (ISNULL(NHAP.TONGNHAP, 0) - ISNULL(XUAT.TONGXUAT, 0)) * SP.DGNHAP AS GIATRITON
    FROM SANPHAM SP
    JOIN (
        SELECT DISTINCT IDSP, IDSIZE, IDMAU
        FROM CHITIETHDN
        UNION
        SELECT DISTINCT IDSP, IDSIZE, IDMAU
        FROM CHITIETHDB
    ) BT ON SP.IDSP = BT.IDSP
    JOIN SIZE SZ ON SZ.IDSIZE = BT.IDSIZE
    JOIN MAU M ON M.IDMAU = BT.IDMAU
    LEFT JOIN (
        SELECT IDSP, IDSIZE, IDMAU, SUM(SOLUONG) AS TONGNHAP
        FROM CHITIETHDN HDN
        JOIN HOADONNHAP HD ON HDN.IDHDN = HD.IDHDN
        WHERE HD.TRANGTHAI = N'Đã nhập'
        GROUP BY IDSP, IDSIZE, IDMAU
    ) NHAP ON SP.IDSP = NHAP.IDSP AND SZ.IDSIZE = NHAP.IDSIZE AND M.IDMAU = NHAP.IDMAU
    LEFT JOIN (
        SELECT IDSP, IDSIZE, IDMAU, SUM(SOLUONG) AS TONGXUAT
        FROM CHITIETHDB
        GROUP BY IDSP, IDSIZE, IDMAU
    ) XUAT ON SP.IDSP = XUAT.IDSP AND SZ.IDSIZE = XUAT.IDSIZE AND M.IDMAU = XUAT.IDMAU
    WHERE 
        (@IDSP IS NULL OR SP.IDSP = @IDSP)
END
